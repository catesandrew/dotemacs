(require 'firestarter)

(defvar-local dotemacs-firestarter-last-status nil
  "The last state of firestarter.")

(defun dotemacs-firestarter-track-state (process)
  "Track the state of a firestarter PROCESS in the corresponding buffer."
  (let ((status (process-status process))
        (return-code (process-exit-status process))
        (buffer (get-buffer (process-get process 'buffer-name))))
    (when (and buffer (buffer-live-p buffer))
      (with-current-buffer buffer
        (let ((status (pcase (list status return-code)
                        (`(exit 0) 'success)
                        (_ 'failure))))
          (setq dotemacs-firestarter-last-status status))
        (force-mode-line-update)))))

(add-hook 'firestarter-reporting-functions #'dotemacs-firestarter-track-state)

(advice-add 'firestarter :before
            (lambda ()
              (setq dotemacs-firestarter-last-status 'running)
              (force-mode-line-update))
            '((name . dotemacs-firestarter-track-running)))

(defun dotemacs-firestarter-mode-line ()
  "Get a mode line status string for firestarter."
  (let ((base "ğŸ”¥")
        (status-string (pcase dotemacs-firestarter-last-status
                         ((guard (not firestarter)) " ")
                         (`running "âš«")
                         (`success "ğŸ”µ")
                         (`failure "ğŸ”´")
                         (_ "âšª"))))
    (concat " " base status-string)))

(provide 'init-firestarter)
